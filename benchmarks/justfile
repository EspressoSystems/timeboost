[private]
build *features:
    cargo build --release --bin run --bin run-committee --features "netns,scenario"
    cargo build --release --workspace --all-targets --features "{{features}}"

[working-directory: ".."]
sailfish-times: (build "until" "times")
    env RUST_LOG=times,error TOKIO_WORKER_THREADS=4 RAYON_NUM_THREADS=4 \
    target/release/run \
        --verbose \
        --env PATH \
        --env HOME \
        --env RUST_LOG \
        --env TOKIO_WORKER_THREADS \
        --env RAYON_NUM_THREADS \
        --spawn "target/release/sailfish \
          --committee 0 \
          --node test-configs/nodes/21R4uDwS7fdxsNPWy92DArC575sYiQdEasFBVEpH8m53e.toml \
          --config-service file:test-configs/nodes/committees/static-5.toml \
          --times-until 10000 \
          --until 20000 \
          --ignore-stamp" \
        --spawn "target/release/sailfish \
          --committee 0 \
          --node test-configs/nodes/23as9Uo6W2AeGronB6nMpcbs8Nxo6CoJ769uePw9sf6Ud.toml \
          --config-service file:test-configs/nodes/committees/static-5.toml \
          --times-until 10000 \
          --until 20000 \
          --ignore-stamp" \
        --spawn "target/release/sailfish \
          --committee 0 \
          --node test-configs/nodes/23oAdU4acQbwSuC6aTEXqwkvQRVCjySzX18JfBNEbHgij.toml \
          --config-service file:test-configs/nodes/committees/static-5.toml \
          --times-until 10000 \
          --until 20000 \
          --ignore-stamp" \
        --spawn "target/release/sailfish \
          --committee 0 \
          --node test-configs/nodes/29iGhwSi5p4zJn2XgGLCwWVU5rCw7aMM2Xk8aJnYnDweU.toml \
          --config-service file:test-configs/nodes/committees/static-5.toml \
          --times-until 10000 \
          --until 20000 \
          --ignore-stamp" \
        target/release/sailfish -- \
          --committee 0 \
          --node test-configs/nodes/eiwaGN1NNaQdbnR9FsjKzUeLghQZsTLPjiL4RcQgfLoX.toml \
          --config-service file:test-configs/nodes/committees/static-5.toml \
          --times-until 10000 \
          --until 10000 \
          --ignore-stamp

[working-directory: ".."]
timeboost-times nodes: (build "times")
    #!/usr/bin/env bash
    set -eo pipefail
    function run_as_root {
        if [ "$CI" == "true" ]; then
            sudo --preserve-env=PATH,HOME,RUST_LOG,TOKIO_WORKER_THREADS,RAYON_NUM_THREADS,TIMEBOOST_NO_SUBMIT "$@"
        else
            run0 --setenv=PATH \
                --setenv=HOME \
                --setenv=RUST_LOG \
                --setenv=TOKIO_WORKER_THREADS \
                --setenv=RAYON_NUM_THREADS \
                --setenv=TIMEBOOST_NO_SUBMIT \
                "$@"
        fi
    }
    export RUST_LOG=times,error TOKIO_WORKER_THREADS=4 RAYON_NUM_THREADS=4 TIMEBOOST_NO_SUBMIT=1
    run_as_root target/release/run \
        --verbose \
        --clear-env \
        --env PATH \
        --env HOME \
        --env RUST_LOG \
        --env TOKIO_WORKER_THREADS \
        --env RAYON_NUM_THREADS \
        --env TIMEBOOST_NO_SUBMIT \
        --uid $(id -u) \
        --gid $(id -g) \
        --spawn "1|anvil --host 11.0.1.0 --port 8545" \
        --run   "2|sleep 3" \
        --spawn "3|target/release/block-maker \
            --bind 11.0.1.0:55000 \
            --committee 0 \
            --config-service file:test-configs/linux/committees/linux-{{nodes}}.toml" \
        --spawn-as-root "4|target/release/run-committee \
            -u $(id -u) \
            -g $(id -g) \
            --committee 0 \
            --nodes test-configs/linux/ \
            --config-service file:test-configs/linux/committees/linux-{{nodes}}.toml \
            --net test-configs/net.toml \
            --scenario test-configs/scenarios/default.toml \
            --verbose \
            --times-until 3000" \
        --spawn "5|target/release/yapper \
            --committee 0 \
            --nodes test-configs/linux/ \
            --config-service file:test-configs/linux/committees/linux-{{nodes}}.toml" \
        target/release/block-checker -- \
            --committee 0 \
            --nodes test-configs/linux/ \
            --config-service file:test-configs/linux/committees/linux-{{nodes}}.toml \
            --blocks 3000

plot-node gnuplot output csv:
    #!/usr/bin/env bash
    set -e
    gnuplot -e "filename=\"{{csv}}\"" "{{gnuplot}}" > "{{output}}"
    xan stats -s "1:" -q "{{csv}}" | xan select mean,q1,median,q3,stddev | xan view -MR -t borderless

plot-durations-per-node header gnuplot dir +nodes:
    for node in {{nodes}}; do \
        echo "# {{header}}"; \
        echo ""; \
        echo "## Node \`${node}\`"; \
        echo ""; \
        echo "![](svg/${node}.svg)"; \
        echo ""; \
        echo "\`\`\`"; \
        just plot-node {{gnuplot}} "{{dir}}/svg/${node}.svg" "{{dir}}/csv/${node}.csv"; \
        echo "\`\`\`"; \
    done
