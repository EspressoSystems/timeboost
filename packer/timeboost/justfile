version         := shell('git rev-parse --short HEAD')
vector-version  := "0.51.1"
nitro-image     := "nitro-node:li-sequencer-catchup"
overlay-archive := "timeboost-overlay.tar.gz"
ssh_user        := "ec2-user"
regions         := '["eu-west-2", "eu-west-3"]'

init:
    packer init .

clean:
    rm -f {{overlay-archive}}

format:
    packer fmt .

build:
    cargo build --release --bin timeboost --features times

[private]
extract-nitro target:
    #!/usr/bin/env bash
    set -e
    podman pull ghcr.io/espressosystems/nitro-espresso-integration/{{nitro-image}}
    MNT=$(podman image mount {{nitro-image}})
    cp $MNT/usr/local/bin/nitro {{target}}
    podman image unmount {{nitro-image}}

make-overlay: build
    #!/usr/bin/env bash
    set -e
    if [[ -f {{overlay-archive}} ]]; then
        rm {{overlay-archive}}
    fi
    dir=$(mktemp -d)
    cp -R overlay $dir/
    mkdir -p $dir/overlay/usr/local/bin
    cp ../../target/release/timeboost $dir/overlay/usr/local/bin/
    run0 just extract-nitro $dir/overlay/usr/local/bin/
    tar --owner 0 --group 0 -caf {{overlay-archive}} -C $dir/overlay/ .

validate:
    packer validate \
        --var version={{version}} \
        --var vector-version={{vector-version}} \
        --var overlay-archive={{overlay-archive}} \
        --var ssh_user={{ssh_user}} \
        --var regions='{{regions}}' \
        timeboost.pkr.hcl

make-image: validate
    packer build \
        --var version={{version}} \
        --var vector-version={{vector-version}} \
        --var overlay-archive={{overlay-archive}} \
        --var ssh_user={{ssh_user}} \
        --var regions='{{regions}}' \
        timeboost.pkr.hcl
