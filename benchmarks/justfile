[private]
build *features:
    cargo build --release --workspace --all-targets --features "{{features}}"

[working-directory: ".."]
sailfish-times: (build "until" "times")
    env RUST_LOG=error TOKIO_WORKER_THREADS=2 RAYON_NUM_THREADS=2 \
    target/release/run \
        --verbose \
        --env PATH \
        --env HOME \
        --env RUST_LOG \
        --env TOKIO_WORKER_THREADS \
        --env RAYON_NUM_THREADS \
        --spawn "1:anvil --port 8545" \
        --run   "2:sleep 3" \
        --run   "3:scripts/deploy-contract -c test-configs/c0/committee.toml -u http://localhost:8545" \
        --spawn "4:target/release/sailfish -c test-configs/c0/node_0.toml --stamp /tmp/stamp-0.sf --ignore-stamp --times-until 10000 --until 20000" \
        --spawn "4:target/release/sailfish -c test-configs/c0/node_1.toml --stamp /tmp/stamp-1.sf --ignore-stamp --times-until 10000 --until 20000" \
        --spawn "4:target/release/sailfish -c test-configs/c0/node_2.toml --stamp /tmp/stamp-2.sf --ignore-stamp --times-until 10000 --until 20000" \
        --spawn "4:target/release/sailfish -c test-configs/c0/node_3.toml --stamp /tmp/stamp-3.sf --ignore-stamp --times-until 10000 --until 20000" \
        target/release/sailfish -- -c test-configs/c0/node_4.toml --stamp /tmp/stamp-4.sf --ignore-stamp --times-until 10000 --until 11000

[working-directory: ".."]
timeboost-times: (build "times")
    env RUST_LOG=error TOKIO_WORKER_THREADS=2 RAYON_NUM_THREADS=2 \
    target/release/run \
        --verbose \
        --env PATH \
        --env HOME \
        --env RUST_LOG \
        --env TOKIO_WORKER_THREADS \
        --env RAYON_NUM_THREADS \
        --spawn "1:anvil --port 8545" \
        --run   "2:sleep 3" \
        --run   "3:scripts/deploy-contract -c test-configs/local/committee.toml -u http://localhost:8545" \
        --spawn "4:target/release/block-maker --bind 127.0.0.1:55000 -c test-configs/local/committee.toml" \
        --spawn "4:target/release/yapper -c test-configs/local/committee.toml" \
        --spawn "5:target/release/run-committee -c test-configs/local/ --times-until 10000" \
        target/release/block-checker -- -c test-configs/local -b 4000

plot unit gnuplot pdf +csv:
    #!/usr/bin/env bash
    set -e
    dir=$(mktemp -p /tmp -d csv-XXXXX)
    inputs=()
    read -a inputs <<< "{{csv}}"
    for i in ${!inputs[@]}; do
        xan rename "delta-$i" -s delta "${inputs[$i]}" -o "$dir/0$i.csv"
    done
    xan cat columns $dir/*.csv | xan select '{{unit}},delta-*' -o "$dir/data.csv"
    gnuplot -e "filename=\"$dir/data.csv\"" "{{gnuplot}}" > "{{pdf}}"
    xan stats -s "1:" -q "$dir/data.csv" | xan select mean,q1,median,q3,stddev | xan view -MR
