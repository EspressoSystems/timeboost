[private]
build *features:
    cargo build --release --bin run --bin run-committee --features "netns"
    cargo build --release --workspace --all-targets --features "{{features}}"

[working-directory: ".."]
sailfish-times: (build "until" "times")
    env RUST_LOG=times,error TOKIO_WORKER_THREADS=4 RAYON_NUM_THREADS=4 \
    target/release/run \
        --verbose \
        --env PATH \
        --env HOME \
        --env RUST_LOG \
        --env TOKIO_WORKER_THREADS \
        --env RAYON_NUM_THREADS \
        --spawn "1:anvil --port 8545" \
        --run   "2:sleep 3" \
        --run   "3:scripts/deploy-contract -c test-configs/c0/committee.toml --max-nodes 5 -u http://localhost:8545" \
        --spawn "4:target/release/sailfish -c test-configs/c0/node_0.toml --stamp /tmp/stamp-0.sf --ignore-stamp --times-until 10000 --until 20000" \
        --spawn "4:target/release/sailfish -c test-configs/c0/node_1.toml --stamp /tmp/stamp-1.sf --ignore-stamp --times-until 10000 --until 20000" \
        --spawn "4:target/release/sailfish -c test-configs/c0/node_2.toml --stamp /tmp/stamp-2.sf --ignore-stamp --times-until 10000 --until 20000" \
        --spawn "4:target/release/sailfish -c test-configs/c0/node_3.toml --stamp /tmp/stamp-3.sf --ignore-stamp --times-until 10000 --until 20000" \
        target/release/sailfish -- -c test-configs/c0/node_4.toml --stamp /tmp/stamp-4.sf --ignore-stamp --times-until 10000 --until 11000

[working-directory: ".."]
timeboost-times nodes: (build "times")
    env RUST_LOG=times,error TOKIO_WORKER_THREADS=4 RAYON_NUM_THREADS=4 \
    run0 --setenv=PATH \
        --setenv=HOME \
        --setenv=RUST_LOG \
        --setenv=TOKIO_WORKER_THREADS \
        --setenv=RAYON_NUM_THREADS \
        target/release/run \
            --verbose \
            --clear-env \
            --env PATH \
            --env HOME \
            --env RUST_LOG \
            --env TOKIO_WORKER_THREADS \
            --env RAYON_NUM_THREADS \
            --uid $(id -u) \
            --gid $(id -g) \
            --spawn "1:anvil --host 11.0.1.0 --port 8545" \
            --run   "2:sleep 3" \
            --run   "3:scripts/deploy-contract -c test-configs/linux/committee.toml --max-nodes {{nodes}} -u http://11.0.1.0:8545" \
            --spawn "4:target/release/block-maker --bind 11.0.1.0:55000 -c test-configs/linux/committee.toml --max-nodes {{nodes}}" \
            --spawn "4:target/release/yapper -c test-configs/linux/ --max-nodes {{nodes}}" \
            --spawn-as-root "5:target/release/run-committee -u $(id -u) -g $(id -g) -c test-configs/linux/ --times-until 10000 --max-nodes {{nodes}}" \
            target/release/block-checker -- -c test-configs/linux -b 10000 --max-nodes {{nodes}}

plot-node gnuplot output csv:
    #!/usr/bin/env bash
    set -e
    gnuplot -e "filename=\"{{csv}}\"" "{{gnuplot}}" > "{{output}}"
    xan stats -s "1:" -q "{{csv}}" | xan select mean,q1,median,q3,stddev | xan view -MR -t borderless

plot-durations-per-node header gnuplot dir +nodes:
    for node in {{nodes}}; do \
        echo "# {{header}}"; \
        echo ""; \
        echo "## Node \`${node}\`"; \
        echo ""; \
        echo "![](svg/${node}.svg)"; \
        echo ""; \
        echo "\`\`\`"; \
        just plot-node {{gnuplot}} "{{dir}}/svg/${node}.svg" "{{dir}}/csv/${node}.csv"; \
        echo "\`\`\`"; \
    done
